---
layout: post
title: Finding the Center of US Counties in SAS
date: 2013-03-04 20:53:43.000000000 -08:00
type: post
parent_id: '0'
published: true
password: ''
status: publish
categories: []
tags:
- centroid
- data step
- geometry
- maps
- proc sql
- Programming
- SAS
meta:
  _edit_last: '1'
  _jetpack_related_posts_cache: a:1:{s:32:"8f6677c9d6b0f903e98ad32ec61f8deb";a:2:{s:7:"expires";i:1543573505;s:7:"payload";a:3:{i:0;a:1:{s:2:"id";i:535;}i:1;a:1:{s:2:"id";i:1069;}i:2;a:1:{s:2:"id";i:173;}}}}
author:
  login: Philihp
  email: philihp@gmail.com
  display_name: Philihp
  first_name: ''
  last_name: ''
---
<p>I had a problem where I needed the center longitude and latitude of each US county. <a href="http://support.sas.com/documentation/cdl/en/graphref/65389/HTML/default/viewer.htm#n0i4k2s0dkf6n8n15222j3gi07o8.htm">SAS comes with some datasets containing map data</a> in the format of tables that trace a line around the borders of counties. I was interested in the <a href="http://support.sas.com/documentation/cdl/en/graphref/65389/HTML/default/viewer.htm#n17k5bnax0zd6qn1p4qvzgd8uz2d.htm">MAPSGFK.US_COUNTIES</a> dataset, which has data that looks like this:</p>
<p><img src="{{ site.baseurl }}/assets/uscounties.png" alt="" title="US Counties Table" width="500" class="alignnone size-full wp-image-1209" /></p>
<p>The State and County numbers were <a href="http://en.wikipedia.org/wiki/FIPS_county_code">FIPS codes</a>, which were also stored in the US_COUNTIES_ATTR table, so those are easy enough to figure out. Segment <i>seemed</i> to be always 1. Resolution and Density were numbers that I figured had to do with how much detail the drawing would have if these points were used in a line drawing.</p>
<p>The longitude and latitude here are all points along the border. The simplest, and most incorrect way of getting the center of the county would be to take an average of them, which would be fine for rectangular counties, but for territories where one side is flat (and has few waypoints) and one side is jagged (such as a coastal border), the midpoint will be weighted to that side. So instead, use geometry.</p>
<p>In geometry, there's a formula for the <a href="http://en.wikipedia.org/wiki/Centroid#Centroid_of_polygon">Centroid of a polygon</a>. This is a magic formula that I just trust is correct, because everything on Wikipedia is true, without exception. Here was my first pass at an implementation of it in SAS:</p>
<pre lang="SAS">proc sort data=mapsgfk.us_counties out=centroids;
  by state county;
run;
data centroids(keep=cx cy county state);
  retain yi yj xi xj a cx cy x0 y0 0;
  set centroids(keep=state county lat long rename=(lat=yj long=xj));
  by state county;
  if(first.county) then do;
    cx = 0;
    cy = 0;
    a = 0;
    x0 = xj;
    y0 = yj;
  end;
  else if(not first.county) then do;
    ta = (xi*yj - xj*yi);
    cx + ((xi+xj)*ta);
    cy + ((yi+yj)*ta);
    a + ta;
  end;
  if(last.county) then do;
    ta = (xj*y0 - x0*yj);
    cx + ((xj+x0)*ta);
    cy + ((yj+y0)*ta);
    a  = ta + a * 0.5;
    cx = cx / (6*a);
    cy = cy / (6*a);
    output;
  end;
  xi = xj;
  yi = yj;
run;</pre>
<p>Here, <i>xi</i> and <i>yi</i> are always the coordinates from the previous point, and "<code>if not first.county</code>"  prevents processing of the first point of a county because it didn't have a previous point. The variables <i>a</i>, <i>cx</i>, and <i>cy</i> accumulate for every point. The variable <i>ta</i> is the area of the rectangle defined by the two points. Once all the points have been accumulated, the area is halfed so it's the actual area, and the area is used in calculating the centroid (<i>cx</i> and <i>cy</i>).</p>
<p><img src="{{ site.baseurl }}/assets/pass1.png" alt="" title="pass1" width="440" height="173" class="aligncenter size-full wp-image-1215" /></p>
<p>But then I noticed the 2nd county was positioned somewhere in the mid-Atlantic. Something had to be up. Looking closer, this was <a href="http://en.wikipedia.org/wiki/Baldwin_County,_Alabama">Baldwin County</a>, which had an island. Looking at the source data, this segment was drawn in two segments, which caused the centroid formula to choke, as it assumed contiguous shapes. So instead, in my second pass, I compute the centroid of every segment, then average them weighted by their areas.</p>
<pre lang="SAS">proc sort data=mapsgfk.us_counties out=centroids;
  by state county segment;
run;
data centroids_temp;
  retain yi yj xi xj a cx cy x0 y0 0;
  set centroids(keep=state county segment lat long rename=(lat=yj long=xj));
  by state county segment;
  if(first.segment) then do;
    cx = 0;
    cy = 0;
    a = 0;
    x0 = xj;
    y0 = yj;
  end;
  else if(not first.segment) then do;
    ta = (xi*yj - xj*yi);
    cx + ((xi+xj)*ta);
    cy + ((yi+yj)*ta);
    a + ta;
  end;
  if(last.segment) then do;
    ta = (xj*y0 - x0*yj);
    cx + ((xj+x0)*ta);
    cy + ((yj+y0)*ta);
    a  = ta + a * 0.5;
    cx = cx / (6*a);
    cy = cy / (6*a);
    output;
  end;
  xi = xj;
  yi = yj;
run;
proc sql;
  create table centroid_weight as
  select
    state, county, sum(a) as sum
  from centroids_temp
    group by state, county;
quit;
proc sql;
  create table centroids as
  select a.state, a.county,
         sum(cx*(a / sum)) as lat,
         sum(cy*(a / sum)) as long
    from centroids_temp a
    inner join centroid_weight b
      on (a.state = b.state and a.county = b.county)
    group by a.state, a.county;
quit;
proc sql;
  drop table centroids_temp;
  drop table centroid_weight;
quit;</pre>
<p>Here, I add two more steps. The first proc SQL block sums up the total area of each county, which is used in the second block to find the average centroid of each county's centroids, weighted by the total area that centroid represents, which gives us correct centroids for counties drawn in multiple segments.</p>
<p><img src="{{ site.baseurl }}/assets/pass2.png" alt="" title="pass2" width="438" height="284" class="alignnone size-full wp-image-1219" /></p>
